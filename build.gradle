import java.util.stream.Collectors

plugins {
  id 'application'
  id 'checkstyle'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.3'
  id 'org.springframework.boot' version '3.1.5'
  id 'org.owasp.dependencycheck' version '8.4.2'
  id 'com.github.ben-manes.versions' version '0.49.0'
  id 'org.sonarqube' version '4.4.1.3373'
  id "com.gorylenko.gradle-git-properties" version "2.4.1"
  id "org.flywaydb.flyway" version "10.0.0"
  id "net.serenity-bdd.serenity-gradle-plugin" version "4.0.16"
}

group = 'com.github.hmcts'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

allprojects {
  sourceCompatibility = '17'
  targetCompatibility = '17'
}

tasks.register('copySerenityToBuild', Copy) {
  from "target/site/serenity"
  into "build/reports/serenity/functional"
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += sourceSets.test.compileClasspath
      compileClasspath += main.output
      runtimeClasspath += sourceSets.test.runtimeClasspath
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += sourceSets.test.compileClasspath
      compileClasspath += main.output
      runtimeClasspath += sourceSets.test.runtimeClasspath
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

task functional(type: Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.29'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
  checkstyleTest.enabled = false
}

jacocoTestReport {
  executionData(test)
  reports {
    xml.required = true
    csv.required = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonarqube'].dependsOn test, jacocoTestReport
project.tasks['copySerenityToBuild'].dependsOn functional, aggregate
project.tasks['functional'].finalizedBy aggregate, copySerenityToBuild

def listFiles(String pattern) {
  return new FileNameFinder()
    .getFileNames("${project.rootDir}", pattern)
    .stream()
    .collect(Collectors.joining(","))
}

sonarqube {
  properties {
    property "sonar.projectName", "HMCTS :: idam-testing-support-api"
    property "sonar.projectKey", "com.github.hmcts:idam-testing-support-api"
    property "sonar.coverage.jacoco.xmlReportPaths", "${listFiles("**/jacocoTestReport.xml")}"
    property "sonar.sources", "src/main"
    property "sonar.exclusions", ["**/uk/gov/hmcts/cft/idam/api/v2/common/model/**",
      "**/uk/gov/hmcts/cft/idam/testingsupportapi/config/**",
      "**/uk/gov/hmcts/cft/idam/testingsupportapi/controllers/**",
      "**/uk/gov/hmcts/cft/idam/api/v2/**",
      "**/uk/gov/hmcts/cft/idam/api/v0/**",
      "**/uk/gov/hmcts/cft/rd/**",
      "**/uk/gov/hmcts/cft/rpe/**"
    ]
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

ext {
  reformLoggingVersion = "6.0.1"
  log4JVersion = "2.21.1"
  springCloudVersion = "2022.0.4"
  flywayVersion = "9.22.3"
  serenityVersion = "4.0.19"
  restAssuredVersion = "5.3.2"
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
  dependencies {
    dependency group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.70'
    // CVE-2018-10237 - Unbounded memory allocation
    dependencySet(group: 'com.google.guava', version: '32.1.3-jre') {
      entry 'guava'
    }
  }
}
ext['tomcat.version'] = '10.1.15'
ext['netty.version'] = '4.1.100.Final' // CVE-2023-44487

repositories {
  mavenCentral()
  maven { url 'https://jitpack.io' }
}


dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-artemis'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
  implementation 'org.springframework.security:spring-security-oauth2-client'
  implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.2.0'
  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: reformLoggingVersion
  implementation group: 'com.github.hmcts', name: 'properties-volume-spring-boot-starter', version: '0.1.1'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion
  implementation 'uk.gov.service.notify:notifications-java-client:4.1.0-RELEASE'
  implementation group: 'org.flywaydb', name: 'flyway-core', version: flywayVersion
  implementation 'org.postgresql:postgresql:42.6.0'
  implementation 'org.apache.commons:commons-collections4:4.4'
  implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.3'
  implementation 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.15.2'
  implementation 'com.google.guava:guava'
  implementation 'io.opentelemetry:opentelemetry-api:1.31.0'
  implementation group: 'org.apache.activemq', name: 'artemis-jms-server', version: '2.31.2'
  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.1.1'

  // CVE fix
  implementation 'org.yaml:snakeyaml:2.2'

  implementation 'org.json:json'
  constraints {
    implementation('org.json:json:20231013') {
      because 'CVE-2023-5072'
    }
  }

  compileOnly group: 'org.projectlombok', name: 'lombok'
  annotationProcessor group: 'org.projectlombok', name: 'lombok'

  testImplementation(platform('org.junit:junit-bom:5.10.1'))
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation 'org.mockito:mockito-core:5.6.0'
  testImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: serenityVersion
  testImplementation group: 'net.serenity-bdd', name: 'serenity-junit5', version: serenityVersion
  testImplementation (group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: serenityVersion) {
    exclude group: 'org.codehaus.groovy', module: 'groovy'
  }
  testImplementation group: 'io.rest-assured', name: 'rest-assured', version: restAssuredVersion
  testImplementation group: 'io.rest-assured', name: 'rest-assured-common', version: restAssuredVersion
  testImplementation group: 'io.rest-assured', name: 'json-path', version: restAssuredVersion
  testImplementation group: 'io.rest-assured', name: 'xml-path', version: restAssuredVersion


  functionalTestImplementation sourceSets.main.runtimeClasspath

  smokeTestImplementation sourceSets.main.runtimeClasspath
}

mainClassName = 'uk.gov.hmcts.cft.idam.testingsupportapi.Application'

bootJar {
  getArchiveFileName().set(provider
    {
      'idam-testing-support-api.jar'
    })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

bootRun {
  systemProperties = System.properties
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

